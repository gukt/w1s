/*
 * Copyright 2018-2019 waiting1s, The Waiting1s Project
 */

// 所有子项目的通用配置
subprojects {
    apply plugin: 'java'

    version '1.0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    ext {
        springVersion = '5.0.7.RELEASE'
        hibernateVersion = '5.3.1.Final'
        druidVersion = '1.1.10'
        mysqlConnectorVersion = '8.0.11'
        springDataJpaVersion = '2.0.8.RELEASE'
        springDataRedisVersion = '2.0.8.RELEASE'
        protobufVersion = '2.4.1'
        jacksonVersion = '2.9.6'
        logbackVersion = '1.2.3'
        slf4jVersion = '1.7.25'
        jodaVersion = '2.10'
        guavaVersion = '23.0'
        hamcrestVersion = '1.3'
        quartzVersion = '2.3.0'
        junitVersion = '4.12'
        poiVersion = '3.17'
        nettyVersion = '4.1.25.Final'
        protostuffVersion = '1.1.3'
        kyroVersion = "4.0.2"
        jedisVersion = '2.9.0'
        javaxPersistenceVersion = '1.0.2'
        zookeeperVersion = '3.4.12'
    }

    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        jcenter()
        mavenCentral()
    }

    // 以下这些依赖是所有子项目都要用到的
    dependencies {
        compile group: 'ch.qos.logback', name: 'logback-classic', version: "$logbackVersion"
        compile group: 'ch.qos.logback', name: 'logback-core', version: "$logbackVersion"

        compile group: 'org.slf4j', name: 'slf4j-api', version: "$slf4jVersion"
        compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: "$slf4jVersion"
        compile group: 'log4j', name: 'log4j', version: '1.2.17'

        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jacksonVersion"
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "$jacksonVersion"
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: "$jacksonVersion"

        compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

        compile group: 'joda-time', name: 'joda-time', version: "$jodaVersion"
        compile group: 'org.codehaus.janino', name: 'janino', version: '3.0.8'
        compile group: 'com.google.guava', name: 'guava', version: "$guavaVersion"
        compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
        compile group: 'org.hamcrest', name: 'hamcrest-all', version: "$hamcrestVersion"
        compile group: 'org.quartz-scheduler', name: 'quartz', version: "$quartzVersion"
        compile group: 'org.aspectj', name: 'aspectjweaver', version: '1.9.1'

        compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.25'

        // ========== testCompile ==========

        testCompile group: 'junit', name: 'junit', version: "$junitVersion"
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: "$hamcrestVersion"
        testCompile group: 'org.springframework', name: 'spring-test', version: "$springVersion"

        // 依赖maven仓库中不存在的jar
        ext.jarTree = fileTree(dir: 'lib', include: '**/*.jar')
        ext.rootProjectLibs = new File(rootProject.rootDir, 'lib').getAbsolutePath()
        ext.jarTree += fileTree(dir: rootProjectLibs, include: '**/*.jar')

        compile jarTree
    }

    jar {
        manifest {
            attributes("Implementation-Title": "Gradle")
        }
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/java', 'src/generated/main/java', 'src/generated/main/grpc'
            }
        }
    }

    // 显示当前项目下所有用于compile的jar.
    task listJars(description: 'Displays all compile jars.') << {
        configurations.compile.each { File file -> println '    ' + file.name }
    }

    task genDefaultDirs(description: 'Generates source dirs') << {
        sourceSets*.java.srcDirs*.each { it.mkdirs() }
        sourceSets*.resources.srcDirs*.each { it.mkdirs() }
    }
}

project(":w1s-api") {
    apply plugin: 'war'

    war.archiveName = "w1s.war"

    dependencies {
        compile group: 'org.springframework.data', name: 'spring-data-jpa', version: "$springDataJpaVersion"
        compile group: 'org.springframework', name: 'spring-webmvc', version: "$springVersion"
        compile group: 'org.hibernate', name: 'hibernate-core', version: "$hibernateVersion"
        compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
        compile group: 'mysql', name: 'mysql-connector-java', version: "$mysqlConnectorVersion"
        compile group: 'com.alibaba', name: 'druid', version: "$druidVersion"
        compile group: 'javax.persistence', name: 'javax.persistence-api', version: '2.2'
        compile group: 'javax.servlet', name: 'jstl', version: '1.2'
        compile group: 'taglibs', name: 'standard', version: '1.1.2'

    }
}